Use DFAllEnt.pkg
Use cCJStandardCommandBarSystem.pkg
Use cCJCommandBarSystem.pkg
Use cDbUpdateHandler.pkg
Use cMyApplication.pkg
Use vWin32fh.pkg

Object oHtmlHelp is a cHtmlHelp
    Set pbAlwaysOnTop to False
End_Object

Object oApplication is a cMyApplication
    Set peHelpType to htHtmlHelp
    Set psProgram to "DUFFileListRepair"
    Set psProduct to "DUF - FileList Repairer"

    Property Handle phoFilelistFixerView
    
    #IF (!@ > 182)
    Object oConnection is a cConnection
        Use LoginEncryption.pkg
        Use DatabaseLoginDialog.dg
    End_Object  
    #ENDIF

End_Object

Object oDbUpdateHandler is a cDbUpdateHandler
    Set pbAutoCreateDbVersionTable to False
    Set pbExitIfDebuggerActive to False
    Set phoDbUpdateHandler of ghoApplication to Self
    // Note: Tell the SQL driver to *not* create cache-files (.cch):
    Set pbUseDriverCacheFiles to False
    Set pbIgnoreDriverUcaseSupport to True

    Procedure OnCreate
        Send SetupIgnoreUcaseSupport
    End_Procedure

    Procedure SetupIgnoreUcaseSupport
        String sDriver
        Integer iDriver
        Get psDriverID to sDriver
        Get DriverIndex sDriver to iDriver
        // Note: If Ignore_Ucase_Support is set to false, the Connectivity Kit will 
        //       behave the same as earlier driver versions.
        //       This means that "U_" columns will be kept during a restructure.
        Set_Attribute DF_DRIVER_IGNORE_UCASE_SUPPORT of iDriver to True
    End_Procedure
    
    Procedure OnPreUpdate
    End_Procedure  

End_Object

Object oToolTipController is a cToolTipController
    Move Self to ghoToolTipController
    Set pbBalloonStyle to False
    Set piIcon to TTI_INFO
    Set psTitle to "Information"
    Set piMaxWidth to 400
    Set piDurationPopup to 14000    // 14 seconds, needed for long tooltips.
End_Object

Use oEditContextMenu.pkg
Use oDEOEditContextMenu17.pkg
Register_Procedure RefreshData

Object oMain is a Panel
    Set Label to (psProduct(ghoApplication))
    Set Location to 92 194
    Set Size to 508 684
    Set piMinSize to 442 625

    Object oCommandBarSystem is a cCJCommandBarSystem
        Set pbTimerUpdate to True
        Set peVisualTheme to xtpThemeOffice2013Outlook
        Set pbAutoResizeIcons to True
        Set pbLargeIcons to True

        Procedure OnCreateCommandBars
            Handle hoOptions
            Get OptionsObject to hoOptions
            Forward Send OnCreateCommandBars
        End_Procedure

        Object oEditToolBar is a cCJToolbar
            Set psTitle to "Edit Toolbar"
            Set pbCloseable to False
            Set pbCustomizable to False
            Set pbShowExpandButton to False

            Object oCutToolbarItem is a cCJCutMenuItem
            End_Object

            Object oCopyToolbarItem is a cCJCopyMenuItem
            End_Object

            Object oPasteToolbarItem is a cCJPasteMenuItem
            End_Object

            Object oPromptToolItem is a cCJPromptMenuItem
                Set pbControlBeginGroup to True
            End_Object

            Object oClearToolItem is a cCJClearMenuItem
                Set psCaption to "Clear"
                Set psToolTip to "Clear Data"
                
                Procedure OnExecute Variant vCommandBarControl
                    Send ClearData of (phoFilelistFixerView(ghoApplication))
                End_Procedure
    
                Function IsEnabled Returns Boolean
                    Function_Return True
                End_Function

            End_Object

            Object oRefreshToolItem is a cCJMenuItem
                Set psImage to "ActionRefresh.ico"  
                Set psToolTip to "Refresh data (F5)"
                Set pbActiveUpdate to True

                Procedure OnExecute Variant vCommandBarControl
                    Send RefreshData of (phoFilelistFixerView(ghoApplication))
                End_Procedure

                Function IsEnabled Returns Boolean
                    String sFileList
                    Get psFileList of (phoFilelistFixerView(ghoApplication)) to sFileList
                    Function_Return (sFileList <> "") 
                End_Function

            End_Object

            Object oOpenContainingFolder_MenuItem is a cCJMenuItem
                Set psImage to "ActionOpenContainingFolder.ico"
                Set psCaption to "Containing Folder"
                Set psToolTip to "Open containing folder"
                Set psDescription to "Open Containing Folder"
                Set pbActiveUpdate to True
                Set pbControlBeginGroup to True

                Procedure OnExecute Variant vCommandBarControl
                    String sPath sFile       
                    Boolean bExists

                    Forward Send OnExecute vCommandBarControl
                    Get psFilelistFrom of ghoApplication to sFile
                    Get ParseFolderName sFile to sPath
                    Get vFilePathExists sFile to bExists
                    // We want to have that file to be selected in Windows Explorer;
                    If (bExists = True) Begin
                        Move ("/select, " + sFile) to sPath
                    End
                    Send vShellExecute "open" "explorer.exe" sPath ""
                End_Procedure

                Function IsEnabled Returns Boolean
                    String sFile
                    Boolean bExists
                    Get psFilelistFrom of ghoApplication to sFile
                    Get vFilePathExists sFile to bExists
                    Function_Return (bExists = True)
                End_Function

            End_Object

            Object oAddToStudio_MenuItem is a cCJMenuItem
                Set psCaption to "Add to Studio"
                Set psDescription to "Add to Studio"
                Set psToolTip to "Add this tool to the DataFlex Studio's 'Tools' menu"
                Set psImage to "ActionAddStudio.ico"
                Set pbControlBeginGroup to True   
                
                Procedure OnExecute Variant vCommandBarControl
                    Forward Send OnExecute vCommandBarControl
                    Set psTag of (oAddToStudio_dg(oAddToStudio_dg(Client_Id(phoMainPanel(ghoApplication))))) ;
                        to "<workspace>"
                    Send Popup of (oAddToStudio_dg(Client_Id(phoMainPanel(ghoApplication))))
                End_Procedure
                
            End_Object

            Object oAbout_MenuItem is a cCJMenuItem
                Set psCaption to "About"
                Set psToolTip to "About Info"
                Set psDescription to "About the program"
                Set psImage to "ActionAbout.ico"
                Set pbControlBeginGroup to True
                Procedure OnExecute Variant vCommandBarControl
                    Forward Send OnExecute vCommandBarControl
                    Send Activate_About of (Client_Id(ghoCommandBars))
                End_Procedure
            End_Object

            Object oKoFi_MenuItem is a cCJMenuItem
                Set psCaption to "Ko-fi"
                Set psToolTip to "Bye me a coffee, to support me"
                Set psDescription to "Bye me a coffee"
                Set psImage to "Ko-fi.ico"
                Set pbControlBeginGroup to True
                Procedure OnExecute Variant vCommandBarControl
                    Runprogram Shell Background "https://ko-fi.com/nilss"
                End_Procedure
            End_Object

            Object oHelpMenuItem is a cCJHelpMenuItem
                Set peControlType to xtpControlSplitButtonPopup
                Set psImage to "ActionHelp.ico"

                Procedure OnExecute Variant vCommandBarControl
                    Send ShowProgramHelp
                End_Procedure

                Object oHelpMenuItemLocal is a cCJMenuItem
                    Set psCaption to "Local HTML Help"
                    Set psImage to "ActionHelp.ico"
                    Procedure OnExecute Variant vCommandBarControl
                        Send ShowProgramHelp
                    End_Procedure
                End_Object

                Object oHelpMenuItemInternetBrowser is a cCJMenuItem
                    Set psCaption to "Online HTML Help"
                    Set psImage to "ActionHelpInternet.ico"
                    Procedure OnExecute Variant vCommandBarControl
                        Runprogram Shell Background "http://www.rdctools.com/HTMLHelpDUF/Developer5.htm"
                    End_Procedure

                End_Object

            End_Object

            Object oExitMenuItem is a cCJExitMenuItem
                Set psToolTip to "Exit application"
                Set psDescription to "Exit the program (Alt+F4)"
                Set psImage to "ActionExit.ico"
                Set pbControlBeginGroup to True
            End_Object

        End_Object

        Object oStatusBar is a cCJStatusBar
            Set StatusBar_Id to Self
            Set psIdleText to "No Action"  

            Object oStatusPane1 is a cCJStatusBarPane
                Set piID to sbpIDIdlePane
                Set pbStyleStretch to True
            End_Object

            Object oStatusPane2 is a cCJStatusBarPane
                Set phoViewPane to Self
                Set pbStyleStretch to True
            End_Object
            
        End_Object

    End_Object

    Object oClientArea is a ClientArea
        Use AddToStudio.dg     
        Use FileListFixerView.vw

        Use StdAbout.pkg
        Procedure Activate_About
            Send DoAbout "" "" ("Copyright 2016-2024" * psCompany(ghoApplication)) "Author: Nils Svedmyr" "DUFFilelistRepairer48.bmp" "E-mail: mailto:support@rdctools.com" "Web-site: www.rdctools.com" ("This tool was created with DataFlex" * String(C_DFVersion) * "from Data Access Worldwide. Please visit http://www.dataaccess.com for a free personal copy of DataFlex.")
        End_Procedure

    End_Object

    // Note that we have overriden the cHtmlHelp "GetHelpFile" function
    // in the oHtmlHelp object, so we need to explitetly send the message
    // to that object.
    Procedure ShowProgramHelp
        Set psHelpFile of ghoApplication to "Developer5.chm"
        Send DoDisplayKeyword of oHtmlHelp "DUF Filelist Repairer"
    End_Procedure

    On_Key Key_F1 Send ShowProgramHelp
End_Object

// *** Note: If you get a compile error: "Can't include the resource COMPILEHEADER.H",
//     you will need to compile the "WriteDateTimeHeaderFile" program first. ***
Start_UI
