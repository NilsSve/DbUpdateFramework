Use cSQLConnectionIniFile.inc

Class cDbUpdateGenericDatabaseDriver is a cObject
    Procedure Construct_Object
        Forward Send Construct_Object
        { MethodType=Property Category="Database" InitialValue="Default" }
        { EnumList="DATAFLEX_ID, MSSQLDRV_ID, DB2_DRV_ID, ODBC_DRV_ID, SQL_DRV, MDSPGSQL, MDSMYSQL, ORA_DRV" }
        Property String psDriverID DATAFLEX_ID
    End_Procedure

    Function ServerKeyword Returns String
        Function_Return CS_SQLIniDSNKeyword
    End_Function

    Function ConstructConnectionString String sServer String sDatabase Boolean bTrusted String sUserID String sPassword Returns String
        String sConnect

        Move (sConnect * ServerKeyword(Self) + "=" + sServer) to sConnect
        If (sDatabase <> "") Begin
            Move (sConnect * ";" + CS_SQLIniDatabaseKeyword + "=" + sDatabase) to sConnect
        End
        If (bTrusted = True) Begin
            Move (sConnect * ";" + CS_SQLIniTrustedKeyword + "=Yes") to sConnect
        End
        Else Begin
            Move (sConnect * ";" + CS_SQLIniUIDKeyword + "=" + sUserID * ";" + CS_SQLIniPWDKeyword + "=" + sPassword) to sConnect
        End
        Function_Return sConnect
    End_Function

    Function DbLogin String sConnectionString String sServer String sDatabase Boolean bTrusted String sUserID String sPassword Returns Boolean
        Boolean bLoginSuccessful
        String sDriverID

        Get psDriverID to sDriverID
        If (sDriverID = DATAFLEX_ID or sDriverID = "") Begin
            Function_Return True
        End
        Move 0 to LastErr  // Do NOT use the Err flag, it will be set true by the login command although it is successful.
        If (bTrusted = False) Begin
            Login sServer sUserId sPassword sDriverID
        End
        Else Begin
            Login sConnectionString "" "" sDriverID
        End
        Move (LastErr = 0) to bLoginSuccessful
        Function_Return bLoginSuccessful
    End_Function
End_Class

Class cDbUpdateMSSQLDriver is a cDbUpdateGenericDatabaseDriver
    Function ServerKeyword Returns String
        Function_Return CS_SQLIniServerKeyword
    End_Function
End_Class

Class cDbUpdateODBCDriver is a cDbUpdateGenericDatabaseDriver
End_Class

Class cDbUpdateDB2Driver is a cDbUpdateGenericDatabaseDriver
    Function DbLogin String sConnectionString String sServer String sDatabase Boolean bTrusted String sUserID String sPassword Returns Boolean
        Boolean bLoginSuccessful
        String sDriverID

        Move False to Err
        Get psDriverID to sDriverID
        If (not(Uppercase(sServer) contains "DSN")) Begin
            Move ("DSN=" + sServer) to sServer
        End
        Login sServer sUserId sPassword sDriverID

        Move (not(Err)) to bLoginSuccessful
        Function_Return bLoginSuccessful
    End_Function
End_Class

Class cDbUpdateDataFlexDriver is a cDbUpdateGenericDatabaseDriver
    Function ConstructConnectionString String sServer String sDatabase Boolean bTrusted String sUserID String sPassword Returns String
        Function_Return ""
    End_Function
End_Class

Class cDbUpdateSQLFLEXDriver is a cDbUpdateGenericDatabaseDriver
    Function DbLogin String sConnectionString String sServer String sDatabase Boolean bTrusted String sUserID String sPassword Returns Boolean
        Boolean bLoginSuccessful
        String sDriverID

        Get psDriverID to sDriverID
        //For the Mertech driver, using the Err indicator is the sanctioned way
        Move False to Err
        If (bTrusted = False) Begin
            Login sServer sUserId sPassword sDriverID
        End
        Else Begin
            Login sServer "" "" sDriverID
        End
        Move (not(Err)) to bLoginSuccessful

        Function_Return bLoginSuccessful
    End_Function
End_Class

Class cDbUpdateORAFLEXDriver is a cDbUpdateGenericDatabaseDriver
    Function DbLogin String sConnectionString String sServer String sDatabase Boolean bTrusted String sUserID String sPassword Returns Boolean
        Boolean bLoginSuccessful
        String sDriverID sErrorMsg
        String sVal
        Integer iPos

        Get psDriverID to sDriverID
        Move (Pos("/", sServer)) to iPos
        If (iPos = 0) Begin
            If (sDatabase <> "") Begin
                Move (sServer + "/" + sDatabase) to sServer
            End
        End
        // For the Mertech driver, using the Err indicator is the sanctioned way
        Move False to Err
        // For Oracle I don't believe "Trusted" doesn't exist.
        Login sServer sUserId sPassword sDriverID

        Move (not(Err)) to bLoginSuccessful
        Function_Return bLoginSuccessful
    End_Function
End_Class

Class cDbUpdateMDSMySQLDriver is a cDbUpdateGenericDatabaseDriver
    Function DbLogin String sConnectionString String sServer String sDatabase Boolean bTrusted String sUserID String sPassword Returns Boolean
        Boolean bLoginSuccessful
        String sDriverID

        Get psDriverID to sDriverID
        // For the Mertech driver, using the Err indicator is the sanctioned way
        Move False to Err
        If (bTrusted = False) Begin
            Login sServer sUserId sPassword sDriverID
        End
        Else Begin
            Login sServer "" "" sDriverID
        End
        Move (not(Err)) to bLoginSuccessful
        Function_Return bLoginSuccessful
    End_Function
End_Class

Class cDbUpdateMDSPgSQLDriver is a cDbUpdateGenericDatabaseDriver
    Function DbLogin String sConnectionString String sServer String sDatabase Boolean bTrusted String sUserID String sPassword Returns Boolean
        Boolean bLoginSuccessful
        String sDriverID

        Get psDriverID to sDriverID
        // For the Mertech driver, using the Err indicator is the sanctioned way
        Move False to Err
        If (bTrusted = False) Begin
            Login sServer sUserId sPassword sDriverID
        End
        Else Begin
            Login sServer "" "" sDriverID
        End
        Move (not(Err)) to bLoginSuccessful
        Function_Return bLoginSuccessful
    End_Function
End_Class

Class cDbUpdateDatabaseDriver is a cObject
    Procedure Construct_Object
        Forward Send Construct_Object
        Property String psDriverID DATAFLEX_ID
        Property Handle phoDriverSpecificObject
    End_Procedure

    Function CreateDriver Returns Handle
        String sDriverID
        Handle hoDriver hcDriverClass

        Get psDriverID to sDriverID
        Case Begin
            Case (sDriverID = MSSQLDRV_ID)
                Move (RefClass(cDbUpdateMSSQLDriver))    to hcDriverClass
                Case Break
            Case (sDriverID = ODBC_DRV_ID)
                Move (RefClass(cDbUpdateODBCDriver))     to hcDriverClass
                Case Break
            Case (sDriverID = DB2_DRV_ID)
                Move (RefClass(cDbUpdateDB2Driver))      to hcDriverClass
                Case Break
            Case (sDriverID = SQLFLEX)
                Move (RefClass(cDbUpdateSQLFLEXDriver))  to hcDriverClass
                Case Break
            Case (sDriverID = ORAFLEX)
                Move (RefClass(cDbUpdateORAFLEXDriver))  to hcDriverClass
                Case Break
            Case (sDriverID = MDSPgSQL)
                Move (RefClass(cDbUpdateMDSPgSQLDriver)) to hcDriverClass
                Case Break
            Case (sDriverID = MDSMySQL)
                Move (RefClass(cDbUpdateMDSMySQLDriver)) to hcDriverClass
                Case Break
            // DATAFLEX_ID = Default driver
            Case Else  
                Move (RefClass(cDbUpdateDataFlexDriver)) to hcDriverClass
                Move DATAFLEX_ID to sDriverID
        Case End

        Get Create hcDriverClass to hoDriver
        Set psDriverID of hoDriver to sDriverID
        Set phoDriverSpecificObject to hoDriver

        Function_Return hoDriver
    End_Procedure

    Procedure DestroyDriver
        If (phoDriverSpecificObject(Self)) Begin
            Send Destroy of (phoDriverSpecificObject(Self))
        End
    End_Procedure

    Function ConstructConnectionString String sServer String sDatabase Boolean bTrusted String sUserID String sPassword Returns String
        String sConnect
        Handle hoDriver

        Get CreateDriver to hoDriver
        Get ConstructConnectionString of hoDriver sServer sDatabase bTrusted sUserID sPassword to sConnect
        Send DestroyDriver
        Function_Return sConnect
    End_Function

    Function DbLogin String sConnectionString String sServer String sDatabase Boolean bTrusted String sUserID String sPassword Returns Boolean
        Boolean bLoginSuccessful
        Handle hoDriver

        Get CreateDriver to hoDriver
        Get DbLogin of hoDriver sConnectionString sServer sDatabase bTrusted sUserID sPassword to bLoginSuccessful
        Send DestroyDriver
        Function_Return bLoginSuccessful
    End_Function

End_Class
