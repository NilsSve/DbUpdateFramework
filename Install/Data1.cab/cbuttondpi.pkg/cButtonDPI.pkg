// Button class that automatically shows the correct icon size for the current DPI-setting:
Class cButtonDPI is a Button
    Procedure Construct_Object 
        Integer iIconSize
        Forward Send Construct_Object
        Get GetCorrectIconSize to iIconSize
        Set piImageSize to iIconSize
    End_Procedure

    // Returns: DPI setting as an integer.
    //  Normal DPI setting: iDPI=96  is "Normal setting" 100%
    //                      iDPI=120 is "Medium setting" 125%
    //                      iDPI= 144 is "Large setting" 150%
    Function GetCurrentDPI Returns Integer
        Handle hDC
        Integer iPixelsX
        Move (GetDC(0)) to hDC
        Move (GetDeviceCaps(hDC, LOGPIXELSX)) to iPixelsX
        Move (ReleaseDC(0, hDC)) to hDC
        Function_Return iPixelsX
    End_Function  
    
    Function GetCorrectIconSize Returns Integer
        Integer iPixelsX iIndex iSize 
        Integer[] iaSizes
        
        Move 16 to iaSizes[0]
        Move 24 to iaSizes[1]
        Move 32 to iaSizes[2]
        Move 48 to iaSizes[3]
        Move 64 to iaSizes[4]
        
        Get piImageSize to iSize  // the "100%" size
        Move (SearchArray(iSize,iaSizes)) to iIndex
        Move (0 max iIndex) to iIndex
        Get GetCurrentDPI to iPixelsX
        // Use a Case Statement here. Wherever we jump in we will continue onwards, regardless of whether
        // we match the remaining Case statements or not. So list our Cases in reverse order, meaning we
        // will increment iIndex as many times as we need here.
        Case Begin
            Case (iPixelsX > 144)
                Increment iIndex
            Case (iPixelsX = 144)
                Increment iIndex
            Case (iPixelsX = 120)
                Increment iIndex  
        Case End
        Move (iIndex min 4) to iIndex
        Function_Return iaSizes[iIndex]
    End_Function
    
//    Function GetCorrectIconSize Returns Integer
//        Integer iPixelsX iRetval
//        Get GetCurrentDPI to iPixelsX
//        Case Begin
//            Case (iPixelsX < 120)
//                Move 16 to iRetval
//                Case Break
//            Case (iPixelsX = 120)
//                Move 24 to iRetval
//                Case Break
//            Case (iPixelsX = 144)
//                Move 32 to iRetval
//                Case Break
//            Case (iPixelsX > 144)
//                Move 48 to iRetval
//                Case Break
//        Case End                
//        Function_Return iRetval
//    End_Function

End_Class
