Use Windows.pkg
Use cCJGridPromptList.pkg
Use cDbUpdateFunctionLibrary.pkg
Use MSSqldrv.pkg
Use db2_drv.pkg
Use odbc_drv.pkg

Object oServerSelection_sl is a ModalPanel
    Set Size to 148 274
    Set Label to "Database Server/DSN Selection"
    Set piMinSize to 89 211
    Set Location to 2 2
    Set Border_Style to Border_Thick

    Property String[] psTheData

    Object oSelList is a cCJGridPromptList
        Set Size to 115 264
        Set Location to 6 6
        Set peAnchors to anAll
        Set pbAllowColumnRemove to False
        Set pbUseAlternateRowBackgroundColor to True
        Set pbGrayIfDisable to False
        Set pbHeaderReorders to False
        Set pbHeaderSelectsColumn to False
        Set pbHeaderTogglesDirection to False
        Set pbShadeSortColumn to False
        Set piFocusCellBackColor to clDkGray

        Object oName is a cCJGridColumn
            Set piWidth to 528
            Set psCaption to "Name"
        End_Object

        Procedure Activating
            tDataSourceRow[] MyData
            Handle hoDataSource
            String[] sTheData
            Integer iCount iSize

            Send Cursor_Wait of Cursor_Control
            Get psTheData to sTheData
            Move (SizeOfArray(sTheData)) to iSize
            Decrement iSize
            For iCount from 0 to iSize
                Move sTheData[iCount] to MyData[iCount].sValue[0]
            Loop

            Forward Send Activating

            Get phoDataSource to hoDataSource
            Send InitializeData of hoDataSource MyData
            Send Cursor_Ready of Cursor_Control
        End_Procedure

    End_Object

    Object oOK_Btn is a Button
        Set Label    to "&OK"
        Set Location to 130 165
        Set peAnchors To anBottomRight

        Procedure OnClick
            Send Ok of oSelList
        End_Procedure

    End_Object

    Object oCancel_Btn is a Button
        Set Label    to "&Cancel"
        Set Location to 130 220
        Set peAnchors to anBottomRight

        Procedure OnClick
            Send Close_Panel
        End_Procedure

    End_Object

    Procedure Page Integer iPageObject
        Set Icon to "ServerLookup.ico"
        Forward Send Page iPageObject
    End_Procedure

    On_Key Key_Alt+Key_O Send KeyAction of oOK_Btn
    On_Key Key_Alt+Key_C Send KeyAction of oCancel_Btn
End_Object
